configurations {
    beaver
}

ext {
    parserDescription = file("${projectDir}/src/main/resources/mc.grammar")
    beaverOutput = file("${projectDir}/src/main/java")
    beaverLibRT = 'net.sf.beaver:beaver-rt:0.9.11'
    beaverLibCC = 'net.sf.beaver:beaver-cc:0.9.11'
}

dependencies {
    beaver ext.beaverLibCC
    compile ext.beaverLibRT
    compile project(':lexer')
}

task createSourceDirectories() << {
    project.beaverOutput.mkdirs()
}

task(generateParser, type: JavaExec) {
    inputs.file project.parserDescription
    outputs.file project.beaverOutput

    classpath = configurations.beaver
    main = 'beaver.comp.run.Make'
    args = [
            "-d", "${project.beaverOutput}",
            "-T",
            "${project.parserDescription}"
    ]
}

generateParser.dependsOn createSourceDirectories

compileScala.dependsOn generateParser

compileScala.taskDependencies.values = compileScala.taskDependencies.values - 'compileJava'
compileJava.dependsOn compileScala

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []